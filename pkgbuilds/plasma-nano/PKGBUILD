# Maintainer: Devin Lin <devin@kde.org>
_mode=host
pkgname=plasma-nano
pkgdesc="A minimal plasma shell package intended for embedded devices."
pkgver=6.0.2.r0.gfc2793c
pkgrel=1
_arches=all
arch=(
    aarch64
    x86_64
    armv7h
)
license=(GPL-2.0-or-later)
depends=(
    plasma-workspace
)
makedepends=(
    cmake
    extra-cmake-modules
)
_commit=fc2793c0693f9ebe8a59903fe6ae4bcb8585da8f
source=(
    "git+https://invent.kde.org/plasma/plasma-nano.git#commit=$_commit"
)
sha256sums=(SKIP)

pkgver() {
    cd "$pkgname"
    git describe --long --abbrev=7 --tags "$_commit" | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    mkdir -p build
}

build() {
    cmake -B build -S "${pkgname}" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_TESTING=OFF \
        -DLIB_INSTALL_DIR=lib \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
        -DCMAKE_BUILD_TYPE=Release
    cmake --build build
}

package() {
    DESTDIR="$pkgdir" cmake --install build
}


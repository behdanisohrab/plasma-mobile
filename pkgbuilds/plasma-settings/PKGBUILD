# Maintainer: Devin Lin <devin@kde.org>
_mode=host
pkgname=plasma-settings
pkgdesc="Settings application for Plasma Mobile"
pkgver=24.02.0.r0.g8d8da9f
pkgrel=1
_arches=all
arch=(
    aarch64
    x86_64
    armv7h
)
license=(GPL-3.0-or-later)
depends=(
    kcmutils
    plasma-workspace
    kdeclarative
    ki18n
    kirigami-addons
)
makedepends=(
    cmake
    extra-cmake-modules
)
_commit=8d8da9f091ea0c3d1caf1546cafa5a18c30cf9af
source=(
    "git+https://invent.kde.org/plasma-mobile/plasma-settings.git#commit=$_commit"
)
sha256sums=(SKIP)

pkgver() {
    cd "$pkgname"
    git describe --long --abbrev=7 --tags "$_commit" | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    mkdir -p build
}

build() {
    cmake -B build -S "${pkgname}" \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib
    cmake --build build
}

package() {
    DESTDIR="$pkgdir" cmake --install build
}


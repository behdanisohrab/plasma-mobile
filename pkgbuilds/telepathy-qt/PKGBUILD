# Maintainer:
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
_mode=host
pkgname="telepathy-qt"
pkgdesc="A library for Qt-based Telepathy clients"
pkgver=0.9.8
pkgrel=3
_arches=all
arch=(
    aarch64
    x86_64
    armv7h
)
license=(LGPL)
url="https://github.com/TelepathyQt/telepathy-qt"
depends=(
    qt5-base
)
makedepends=(
    cmake
    doxygen
    python
    qt5-tools
)

_pkgsrc="$pkgname-$pkgver"
_pkgext="tar.gz"
source=(
  "https://telepathy.freedesktop.org/releases/$pkgname/$_pkgsrc.$_pkgext"{,.asc}
)
sha256sums=(
  'bf8e2a09060addb80475a4938105b9b41d9e6837999b7a00e5351783857e18ad'
  'SKIP'
)
validpgpkeys=('AA33B0D27868E36C151780F0FE0B6D736B1195ED') # Alexandr Akulich

build() {
  local _cmake_options=(
    -B build
    -S "$_pkgsrc"

    -DCMAKE_INSTALL_PREFIX=/usr
    -DENABLE_EXAMPLES=OFF
    -DENABLE_TESTS=OFF
  )

  cmake "${_cmake_options[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

